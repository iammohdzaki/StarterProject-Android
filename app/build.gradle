plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    buildFeatures {
        viewBinding true
    }
    def appName = 'Starter Project'
    flavorDimensions "none"
    defaultConfig {
        applicationId "com.starter"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    productFlavors{
        dev{
            buildConfigField 'String', 'BASE_URL', '"https://starterproject.free.beeceptor.com/"'
            buildConfigField 'boolean', 'WATER_MARK', 'true'
        }
        live{
            buildConfigField 'String', 'BASE_URL', '"http://65.2.56.48:3001/"'
            buildConfigField 'boolean', 'WATER_MARK', 'false'
        }
    }
    applicationVariants.all{ variant ->
        variant.outputs.all { output ->
            outputFileName = ("${appName}_${variant.productFlavors[0].name}_${buildType.name}_v" +
                    "${defaultConfig.versionCode}").toLowerCase().replace(" ", "_") + ".apk"
        }
    }
}

dependencies {
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'

    //UI
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    def androidxActivity = '1.3.1'
    def androidxFragment = '1.3.6'
    implementation "androidx.activity:activity-ktx:$androidxActivity"
    implementation "androidx.fragment:fragment-ktx:$androidxFragment"

    //Navigation
    def nav_version = "2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //Facebook Shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    //Animation Library
    implementation "com.airbnb.android:lottie:3.3.0"

    //View Pager 2
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    //Retrofit
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'org.conscrypt:conscrypt-android:2.2.1'

    //For Image Loading
    def glide_version = '4.11.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    //Lifecycle
    def androidxLifecycle = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycle"

    //Room DB
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation("androidx.room:room-ktx:$room_version")

    //DataStore
    def dataStore_version = "1.0.0"
    implementation "androidx.datastore:datastore-preferences-core:$dataStore_version"

    //Hilt
    implementation("com.google.dagger:hilt-android:$hilt_version")
    kapt("com.google.dagger:hilt-android-compiler:$hilt_version")

    //Unit Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.3"

    //UI Test
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "com.google.truth:truth:1.1.3"
}

kapt {
    correctErrorTypes true
}